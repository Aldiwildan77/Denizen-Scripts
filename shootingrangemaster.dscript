# This thing will manage the shooting range, sending carts and such.
# It was written by Wahrheit and you better believe it
# @author Wahrheit
# @version 1.0
# @last-updated May 20 2013

"Shooting Range":
    type: assignment
    interact scripts:
    - 10 Target Practice
    actions:
        on assignment:
        - runtask 'script:Slingshot'

"Range Master Format":
    type: format
    format: "<gray>Range Master<white><&co> <text>"

"Slingshot":
    type: task
    script:
    - teleport npc 'location:<anchor:sr1>'
    - trigger name:proximity toggle:true cooldown:0.1s radius:6
    - trigger name:chat toggle:true cooldown:0.1s radius:6

"Minecart Listener":
    type: world
    events:
        on player kills minecart:
        - if npcid:68 "<npc.flag[range-time]>" == "start" && "<player.name>" == "<npc.flag[active-player]>" flag npc "minecarts-hit:++"
    
"Begin Game":
    type: task
    script:
    - flag npc minecarts-hit:0
    - flag npc range-runs:0
    - flag npc active-player:<player.name>
    - flag npc "range-level:->:1"
    - flag npc "range-level:->:2"
    - flag npc "range-level:->:3"
    - flag npc "range-level:->:4"
    - flag npc "range-level:->:5"
    - flag npc range-time:start
#    - flag npc range-level:<util.random.int[1].to[5]>
    
"Roll Carts":
    type: task
    script:
    - if "<npc.flag[range-runs] || 0>" < 5 {
      - flag npc range-runs:++
      - if "<npc.flag[range-level] || 0>" > 0
        flag npc "range-level:->:<npc.flag[current-level]>"
      - flag npc "range-level:<util.random.int[1].to[5]>"
      - flag npc "current-level:<npc.flag[range-level].random>"
      - switch location:<anchor:Minecart<npc.flag[current-level]><util.random.int[1].to[2]>> state:on duration:1
      - flag npc "range-level:<-:<npc.flag[current-level]>"
      - runtask "script:Roll Carts" delay:6 queue:<util.random.uuid> }
      else {
      - flag npc range-runs:!
      - flag npc range-level:!
      - flag npc range-winnings:<m:<npc.flag[minecarts-hit]>*3>
      - narrate "format:Range Master Format" "Time's up! You won '<npc.flag[range-winnings]>' gold!"
      - give money qty:<npc.flag[range-winnings]>
      - execute as_server warp <player.name> shootingrange
      - zap 'script:Target Practice' 'step:In Range' }

"Target Practice":
    type: interact
    steps:
        'In Range*':
            proximity trigger:
                entry:
                    radius: 6
                    script:
                    - if <player.money> > 8 narrate "format:Range Master Format" "Hey there <player.name>! Come to practice your aim? It'll cost you 8 gold, but I'll give you 2 for each target! Right click me to start."
                    - if <player.money> == 8 narrate "format:Range Master Format" "Hey there <player.name>! Looks like you've got just enough to play a round, are you up for it? It'll cost 8 gold, but I'll give you 2 per target! Right click me to start."
                    - if <player.money> < 8 narrate "format:Range Master Format" "Hey there <player.name>! Doesn't look like you have enough cash to play, come back later and take a shot or two."
                exit:
                    radius: 6
                    script:
                    - narrate "format:Range Master Format" "Thanks for visiting, come back soon."
            click trigger:
                script:
                - narrate "format:Range Master Format" "Click me once more to get started!"
                - zap 'step:Game Start'
        'Game Start':
            click trigger:
                script:
                - narrate "format:Range Master Format" "Okay, you've got 1 minute! Good luck!"
                - execute as_server "warp <player.name> shootingrange-1"
                - runtask "script:Begin Game" instant
                - runtask "script:Roll Carts"