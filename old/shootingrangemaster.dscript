# This thing will manage the shooting range, sending carts and such.
# It was written by Wahrheit and you better believe it
# @author Wahrheit
# @version 1.1
# @last-updated August 7th 2013

"Shooting Range":
    type: assignment
    interact scripts:
    - 10 Target Practice
    actions:
        on assignment:
        - runtask 'script:Slingshot'

"Range Master Format":
    type: format
    format: "<gray>Range Master<white><&co> <text>"

"Slingshot":
    type: task
    script:
    - teleport npc 'location:<anchor:sr1>'
    - trigger name:proximity toggle:true cooldown:0.1s radius:6
    - trigger name:chat toggle:true cooldown:0.1s radius:6

"Minecart Listener":
    type: world
    events:
        on player destroys minecart:
#        - announce "Minecart was destroyed!"
        - if "<npc[68].flag[range-runs] || 0>" > 0
          && "<player.name>" == "<npc[68].flag[active-player]>"
          flag npcid:68 npc "minecarts-hit:++"

"Roll Carts":
    type: task
    script:
    - if "<npc.flag[range-runs] || 0>" < 10 {
      - flag npc range-runs:++
      - switch location:<anchor:Minecart<util.random.int[1].to[5]><util.random.int[1].to[2]>> state:on duration:1
      - runtask "script:Roll Carts" delay:6 queue:<util.random.uuid> }
      else {
      - flag npc range-runs:!
      - flag npc range-level:!
      - flag npc range-winnings:<m:<npc.flag[minecarts-hit]>*3>
      - narrate "format:Range Master Format" "Time's up! You won '<npc.flag[range-winnings]>' gold!"
      - give money qty:<npc.flag[range-winnings]>
      - execute as_server "warp <player.name> shootingrange"
      - zap "script:Target Practice" "step:In Range" }

"Target Practice":
    type: interact
    steps:
        'In Range*':
            proximity trigger:
                entry:
                    radius: 6
                    script:
                    - if <player.money> > 10 narrate "format:Range Master Format" "Hey there <player.name>! Come to practice your aim? It'll cost you 10 gold, but I'll give you 3 for each target! Right click me to start."
                    - if <player.money> == 10 narrate "format:Range Master Format" "Hey there <player.name>! Looks like you've got just enough to play a round, are you up for it? It'll cost 10 gold, but I'll give you 3 per target! Right click me to start."
                    - if <player.money> < 10 narrate "format:Range Master Format" "Hey there <player.name>! Doesn't look like you have enough cash to play, come back later and take a shot or two."
#                    - narrate "format:Range Master Format" "Hey <player.name>, click me if you'd like to shoot!"
                exit:
                    radius: 6
                    script:
                    - narrate "format:Range Master Format" "Thanks for visiting, come back soon."
            click trigger:
                script:
                - if <player.money> >= 10 narrate "format:Range Master Format" "Click me once more to get started. Make sure you have a bow and arrows!" else narrate "format:Range Master Format" "Sorry, you don't have 10 gold so you can't shoot!"
                - if <player.money >= 10 zap 'step:Game Start'
        'Game Start':
            click trigger:
                script:
                - take money qty:10
                - narrate "format:Range Master Format" "Okay, you've got 1 minute! Good luck!"
                - execute as_server "warp <player.name> shootingrange-1"
                - flag npc active-player:<player.name>
                - flag npc minecarts-hit:0
                - runtask "script:Roll Carts"