# This thing handles the dude that sits at the entrance to the Grand Library portal
# It was written by Wahrheit and you better believe it
# @author Wahrheit
# @version 1.0
# @last-updated March 9 2014

"Maze Master":
    type: assignment
    interact scripts:
    - 10 Librarian
    actions:
        on assignment:
        - run "Library Assign" instant
"Librarian Format":
    type: format
    format: "<gray>Grand Librarian<white><&co> <text>"
"Library Assign":
    type: task
    script:
    - teleport npc 'location:<anchor:mazemaster>'
    - trigger name:proximity toggle:true radius:3
    - flag n@94 mazeclear:1
#"12 Hour Reset":
#    type: world
#    events:
#        on time changes in w@prosperus:
#        - if <util.date.time.hour> == "0" && !<n@94.flag[resetrun]> {
#          - run Reset
#          - flag n@94 resetrun duration:1h
#          }
#          else if <util.date.time.hour> == "12" && !<n@94.flag[resetrun]> {
#          - run Reset
#          - flag n@94 resetrun duration:1h
#          }
"Maze Tracker":
    type: world
    events:
        on player dies:
        - if <player.location.world> == 'w@prosperus' && <player.name> == <n@94.flag[maze-challenger]> {
          - flag n@94 mazeclear:1
          - announce "<red><player.name> has been conquered by the Grand Library. It is now clear."
          - flag n@94 maze-challenger:! 
          }
        on player changes world from w@prosperus:
        - if <player.location.world> != 'w@prosperus' && <player.name> == <n@94.flag[maze-challenger]> {
          - flag n@94 mazeclear:1
          - announce "<red><player.name> has abandoned the Grand Library. It is now clear."
          - flag n@94 maze-challenger:! 
          }
        on player quits:
        - if <player.name> == <n@94.flag[maze-challenger]> {
          - flag n@94 mazeclear:1
          - flag n@94 maze-challenger:!
          - determine passive "<red><player.name> has vanished into the mists and abandoned the Grand Library. It is now clear."         
          - flag n@94 "abandoner:->:<player.name>"
          }
        on player joins:
        - if <player.name> == <n@94.flag[abandoner]> {
          - execute as_server "warp <player.name> spawn"
          - flag n@94 "abandoner:<-:<player.name>"
          - narrate "<red>You disconnected while in the Grand Library and have been returned to spawn."
          - flag n@94 mazeclear:1
          }
"Player Reset":
    type: task
    script:
    # Clear the maze
    - teleport npc 'location:<anchor:maze-gen>'
    - execute as_npc "warp <player.name> library-paste"
    - wait 1
    - execute as_op "/schem load library-blank"
    - execute as_op "/paste"
    - execute as_npc "warp <player.name> library-maze"
    - execute as_op "cs maze bookshelf 25 25"
    - execute as_op "region sel library-maze"
    - execute as_op "/replace glass air"
    - execute as_op "/sel"    
    - teleport npc 'location:<anchor:mazemaster>'
    
"Librarian":
    type: interact
    steps:
        'Greeting*':
            proximity trigger:
                entry:
                    radius: 3
                    script:
                    - narrate "format:Librarian Format" "Hello, <player.name>. Welcome to the Grand Library."
                    - zap 'step:Entrance'
#            click trigger:
#                script:
#                - narrate "format:Librarian Format" "You're at the step 'Greeting'! This is a debug message!"

        'Entrance':
            click trigger:
                script:
                - narrate "format:Librarian Format" "The Grand Library magically reforms itself - it is as alive as any other being in this world."
                - narrate "format:Librarian Format" "Twice each day, bold adventurers are rewarded for traversing its halls. Are you up to the challenge?"
                - zap 'step:Verify'
            proximity trigger:
                exit:
                    radius: 3
                    script:
                    - zap 'step:Greeting'
        'Verify':
            click trigger:
                script:
                - if <flag.npc:mazeclear> == 1 {
                  - narrate "format:Librarian Format" "Very well. I wish you the very best of luck in your efforts."
                  - flag n@94 mazeclear:!
                  - flag n@94 maze-challenger:<player.name>
                  - narrate "<yellow>The Librarian begins chanting a spell..."
                  - run "Player Reset" instant
                  }
                  else {
                  - narrate "format:Librarian Format" "Sorry, it appears the temple is currently being challenged by <flag.npc:maze-challenger>. You will have to wait."
                  - narrate "format:Librarian Format" "Debug <flag.npc:mazeclear>"
                  }
            proximity trigger:
                exit:
                    radius: 3
                    script:
                    - zap 'step:Greeting'